import java.util.regex.Pattern
import com.android.manifmerger.*
import com.android.utils.*
import com.google.common.io.Files
import com.google.common.base.Charsets

buildscript {
    repositories {
        google()
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.1'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.10'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
    buildDir = rootProject.projectDir.toString() + "/build/${project.name}"
}

gradle.ext.blacklist = [
  'dialer/binary/google',
  'dialer/constants/googledialer',
  'incallui/maps/impl',
  'incallui/calllocation/impl',
  'contacts/common/format/testing',
]

gradle.ext.reslist = [
  'contacts/common',
  'dialer/calllog/',
  'dialer/app',
  'dialer/blocking',
  'dialer/lookup',
  'voicemail/impl/configui',
  'incallui/video/impl',
  'incallui/maps/impl',
  'dialer/calldetails',
  'dialer/voicemail/listui/error',
  'incallui/disconnectdialog'
]

subprojects{ subproject ->
    if (subproject.path == ':Lineage_Dialer') {

      apply plugin:  'com.android.application'
      apply plugin: 'com.google.protobuf'

      android {
          aaptOptions {
            def packages = file(subproject.projectDir.path + '/packages.mk').text
            .findAll(Pattern.compile(/(([A-Za-z]{1}[A-Za-z\d_]*\.)+[A-Za-z][A-Za-z\d_]*)/,Pattern.MULTILINE)) - 'AndroidManifest.xml'                  

            additionalParameters = []

            packages.each {
              additionalParameters << '--extra-packages'
              additionalParameters << it
            }
            //additionalParameters << '--no-xml-namespaces'
          }
          packagingOptions {
              exclude 'META-INF/DEPENDENCIES'
              exclude 'META-INF/INDEX.LIST'
              exclude 'META-INF/LICENSE'
              exclude 'META-INF/LICENSE.txt'
              exclude 'META-INF/license.txt'
              exclude 'META-INF/NOTICE'
              exclude 'META-INF/NOTICE.txt'
              exclude 'META-INF/notice.txt'
              exclude 'META-INF/ASL2.0'
              exclude 'META-INF/io.netty.versions.properties'
          }

          sourceSets {
              main {
                  java {
                      srcDirs = [subproject.projectDir.path + '/java']
                      gradle.blacklist.each {
                              exclude ('**/' + it + '/*.java')
                          }
                      srcDirs += (rootProject.projectDir.toString() + '/patch')
                  }

                  res {
                    subproject.projectDir.eachDirRecurse { dir ->
                      if(dir.name == 'res' && !gradle.reslist.any{dir.path.contains(it + '/res')} && !gradle.blacklist.any{dir.path.contains(it + '/res')}) {
                        srcDir dir.path
                      }
                    }
                  }

                  manifest {
                      def manifests = fileTree(subproject.projectDir.path){
                        include '**/AndroidManifest.xml'
                        exclude subproject.projectDir.path + 'AndroidManifest.xml'
                        gradle.blacklist.each {
                          exclude "**/${it}/AndroidManifest.xml"
                        }
                      }.files

                      ILogger ilogger = new StdLogger(StdLogger.Level.ERROR)

                      ManifestMerger2.Invoker invoker = ManifestMerger2.newMerger(file(subproject.projectDir.path + '/AndroidManifest.xml'), ilogger, ManifestMerger2.MergeType.APPLICATION);
                      manifests.each {
                        invoker.addLibraryManifests(it)
                      }
                          
                      MergingReport mReport = invoker.merge() 
                      
                      if(mReport.result.SUCCESS) {
                        Files.write(mReport.getMergedXmlDocument(MergingReport.MergedManifestKind.MERGED).prettyPrint(), file('MergedAndroidManifest.xml'), Charsets.UTF_8)
                      }
                      srcFile 'MergedAndroidManifest.xml'
                  }

                  proto {
                      srcDirs = [subproject.projectDir.toString()]
                  }
              }

              debug {
                res {
                  subproject.projectDir.eachDirRecurse { dir ->
                    if(dir.name == 'res' && gradle.reslist.any{dir.path.contains(it + '/res')} && !gradle.blacklist.any{dir.path.contains(it + '/res')}) {
                      srcDir dir.path
                    }
                  }
                }
              }

              release {
                res {
                  subproject.projectDir.eachDirRecurse { dir ->
                    if(dir.name == 'res' && gradle.reslist.any{dir.path.contains(it + '/res')} && !gradle.blacklist.any{dir.path.contains(it + '/res')}) {
                      srcDir dir.path
                    }
                  }
                }
              }
          }
      }
      configurations {
        compile.exclude group: 'com.google.protobuf', module: 'protobuf-java'
      }
      protobuf {
          protoc {
              artifact = 'com.google.protobuf:protoc:3.9.2'
          }
          plugins {
              javalite {
                  artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
              }
          }
          generateProtoTasks {
              all().each { task ->
                  task.builtins {
                      remove java
                  }
                  task.plugins {
                      javalite {}
                  }
              }
          }
      }      
      dependencies {
        compileOnly files(rootProject.projectDir.toString() + '/lib/lineage-android.jar')
        
        implementation project(":frameworks_ex:common")
        implementation project(":libbackup")

        implementation 'com.android.support:support-core-ui:28.0.0'
        implementation 'com.android.support:support-v13:28.0.0'
        implementation 'com.android.support:support-v4:28.0.0'
        implementation 'com.android.support:support-v4:28.0.0'
        implementation 'com.android.support:appcompat-v7:28.0.0'
        implementation 'com.android.support:recyclerview-v7:28.0.0'
        implementation 'com.android.support:cardview-v7:28.0.0'
        implementation 'com.android.support:design:28.0.0'

        implementation "com.google.guava:guava:23.0"
        implementation 'com.google.errorprone:error_prone_annotations:2.0.18'

        implementation 'com.google.dagger:dagger:2.7'
        annotationProcessor 'com.google.dagger:dagger-compiler:2.7'
        
        implementation 'com.github.bumptech.glide:disklrucache:4.9.0'
        implementation 'com.github.bumptech.glide:gifdecoder:4.9.0'
        implementation 'com.github.bumptech.glide:glide:4.9.0'
        implementation 'com.github.bumptech.glide:annotations:4.9.0'
        annotationProcessor "com.github.bumptech.glide:compiler:4.9.0"

        implementation "javax.annotation:javax.annotation-api:1.2"
        implementation "me.leolin:ShortcutBadger:1.1.13"
        implementation "javax.inject:javax.inject:1"
        implementation 'commons-io:commons-io:2.4'
        implementation 'org.apache.james:apache-mime4j-core:0.7.2'
        implementation 'org.apache.james:apache-mime4j-dom:0.7.2'

        implementation 'io.grpc:grpc-core:1.0.3'
        implementation 'io.grpc:grpc-protobuf-lite:1.0.3'
        implementation 'io.grpc:grpc-okhttp:1.0.3'
        implementation 'io.grpc:grpc-stub:1.0.3'
        implementation 'io.grpc:grpc-all:1.0.3'
        implementation 'io.grpc:grpc-context:1.0.3'

        implementation "com.googlecode.libphonenumber:libphonenumber:8.9.5"
        implementation "com.googlecode.libphonenumber:geocoder:2.94"
        implementation "commons-io:commons-io:2.6"

        implementation "com.android.volley:volley:1.1.1"
        
        implementation "com.google.zxing:core:3.4.0"

        implementation "com.google.auto.value:auto-value:1.5.2"
        annotationProcessor "com.google.auto.value:auto-value:1.5.2"

        implementation fileTree(dir: rootProject.projectDir.toString() + "/lib", include: ['lineage-sdk.jar','*.aar'])
      }
      gradle.projectsEvaluated {
          tasks.withType(JavaCompile) {
              options.compilerArgs.add('-Xbootclasspath/p:' + rootProject.projectDir.path + '/lib/lineage-android.jar')
          }
      }
    } else {
      apply plugin: 'com.android.library'
    }
    android {
      compileSdkVersion 28

      lintOptions { 
          disable 'MissingTranslation','ExtraTranslation','NotSibling','ProtectedPermissions','AppCompatResource','ImpliedQuantity','RtlCompat','AppLinkUrlError','MissingDefaultResource','StringFormatMatches','UsesMinSdkAttributes'
          abortOnError false
      }

      defaultConfig {
          minSdkVersion 28
          targetSdkVersion 28
          versionCode 1
          versionName "1.0"
      }

      compileOptions {
          sourceCompatibility JavaVersion.VERSION_1_8
          targetCompatibility JavaVersion.VERSION_1_8
      }
    }
}